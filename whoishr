#!/usr/bin/perl
# by Matija Nalis <mnalis-perl@voyager.hr> GPLv3+ started 2017-09-26
# parses .hr domain registry by scraping web, as it contains more info then WHOIS client

use warnings;
use strict;

use LWP::UserAgent;
use HTML::TreeBuilder::XPath;

my $domain = $ARGV[0];
if (!defined $domain) {
	print "Usage: $0 <domain.hr>\n";
	exit 1;
}

if ($domain =~ /^([a-z0-9\-]{1,64}\.hr)$/i) {
	$domain = $1;
} else {
	print "Domain '$domain' does not look like .hr domain\n";
	exit 2;
}

my $req_url = 'https://dns.hr/portal/webwhois?domain=' . lc($domain);


my $ua = LWP::UserAgent->new;
my $req = HTTP::Request->new(GET => $req_url);
my $content = $ua->request($req)->as_string;


my $tree= HTML::TreeBuilder::XPath->new;
$tree->parse_content($content);

my @IDs = qw (
owner-name
owner-address
owner-email
admin-name
admin-email
tech-name
tech-email
records
registrar
expire
);

# trims all whitespace from beginning and end of string
sub alltrim($) {
	my ($s) = @_;
	$s =~ s/^\s*//;
	$s =~ s/\s*$//;
	return $s;
}

sub getid($) {
	my ($id) = @_;
	return alltrim($tree->findvalue( '//*[@id="whois-' . $id . '"]'));
}

foreach my $id (@IDs) {
	print "$id:\t" . getid($id)  . "\n";
}
