#!/usr/bin/perl
# by Matija Nalis <mnalis-perl@voyager.hr> GPLv3+ started 2017-09-26
# parses .hr domain registry by scraping web, as it contains more info then WHOIS client

use warnings;
use strict;

use LWP::UserAgent;
use HTML::TreeBuilder::XPath;

my $domain = $ARGV[0];
if (!defined $domain) {
	print "Usage: $0 <domain.hr>\n";
	exit 1;
}

if ($domain =~ /^([a-z0-9\-]{1,64}\.hr)$/i) {
	$domain = lc($1);
} else {
	print "WARNING: Domain '$domain' does not look like .hr domain, calling regular whois...\n\n";
	exec "whois", $domain;
	die "whois(1) does not seem to be installed, aborting";
}

my $req_url = 'https://dns.hr/portal/webwhois?domain=' . $domain;


# fetch HTML
my $ua = LWP::UserAgent->new;
my $req = HTTP::Request->new(GET => $req_url);
my $content = $ua->request($req)->as_string;

# parse HTML
my $tree= HTML::TreeBuilder::XPath->new;
$tree->parse_content($content);

# trims all whitespace from beginning and end of string
sub alltrim($) {
	my ($s) = @_;
	$s =~ s/^\s*//;
	$s =~ s/\s*$//;
	return $s;
}

# fetch one whois-xxxx id from HTML
sub get_id {
	my ($id, $extra_xpath) = @_;
	$extra_xpath = '' if !defined $extra_xpath;
	if (wantarray) {
		return $tree->findnodes_as_strings (qq !//*[\@id="$id"]$extra_xpath!);
	} else {
		return alltrim($tree->findvalue (qq !//*[\@id="$id"]$extra_xpath!));
	}
}

# print one (or more) values from HTML for specified IDs
sub print_id {
	my (@IDs) = @_;
	foreach my $id (@IDs) {
		print "$id:\t" . get_id("whois-$id")  . "\n";
	}
}

print "HTML-source:\t$req_url\n";
print "domain-name:\t$domain\n";
print_id('owner-name', 'owner-address', 'owner-email');
print "admin-contact:\t" . get_id('whois-admin-name') . ' <' . get_id('whois-admin-email') . ">\n";
print "tech-contact:\t" .  get_id('whois-tech-name')  . ' <' . get_id('whois-tech-email')  . ">\n";

my $XPATH_SKIP_DESC='/p[not(substring-before(., ":"))]';

print "domain-expire:\t" . get_id('whois-expire',    $XPATH_SKIP_DESC) . "\n";
print "registrar:\t" .     get_id('whois-registrar', $XPATH_SKIP_DESC) . "\n";

my @ns_records = get_id('whois-records', "${XPATH_SKIP_DESC}/text()");
foreach my $ns (@ns_records) {
	print "name-server:\t" . $ns . "\n";
}
